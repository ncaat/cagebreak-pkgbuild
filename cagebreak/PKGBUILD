# Maintainer: project-repo <archlinux-aur@project-repo.co>
pkgname=cagebreak
pkgver=1.9.1
pkgrel=1
pkgdesc='Tiling wayland compositor based on cage inspired by ratpoison'
arch=('x86_64')
url='https://github.com/project-repo/cagebreak'
license=('MIT')
depends=('wayland' 'libxkbcommon' 'wlroots<0.16.0' 'pango')
makedepends=('meson' 'ninja' 'scdoc' 'wayland-protocols')
optdepends=('wl-clipboard: clipboard support'
            'xorg-xwayland: x application support')
options=('!buildflags' '!strip')
conflicts=('cagebreak-git' 'cagebreak-bin')
provides=('cagebreak')
source=("$pkgname-$pkgver.tar.gz::https://github.com/project-repo/cagebreak/releases/download/$pkgver/release_$pkgver.tar.gz")
sha512sums=('563e985791b4d206f5cf0dc98d8060bd54fd076801a8b56a8f81447ab11f219f352c73ccd4d0113aae7a5dd3d9ff8aee6a166e31b4c2d6705c08dddf479f997a')
build() {
	cd "$pkgname"
	meson build --buildtype=release -Dman-pages=true -Dxwayland=true
	ninja -C build
}
package() {
	cd "$pkgname"
        install -Dt "$pkgdir/usr/bin/" 'build/cagebreak'
        install -Dt "$pkgdir/usr/share/licenses/$_name/" 'LICENSE'
        for f in 'build/cagebreak.1' 'build/cagebreak-config.5'; do
                install -Dt "$pkgdir/usr/share/man/man${f:(-1)}/" "$f"
        done
}
